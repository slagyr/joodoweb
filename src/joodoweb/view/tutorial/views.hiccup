[:h3 "Working With Our Views"]

[:h4 "Testing The View Helpers File"]
[:p "Next thing we want to do is list all of the blog posts on the index page. To do that, we will interact with our view_helpers file. Before we add any functionality, let's create a " [:b "view_helpers_spec.clj"] " file in the " [:b "spec/sample_app/view"] " directory, and fill it with our first test:"]
[:pre {:class "brush: clojure"}
"(ns sample-app.view.view-helpers-spec
  (:require [speclj.core :refer [describe it should= run-specs around]]
            [sample-app.view-helpers :refer [get-post-name]]))

(describe \"view_helpers\"
  (it \"gets the title of a post\"
    (should= \"test post 1\"
             (get-post-name \"20111215_test-post-1.hiccup\"))))"]

[:p "In this test we are asserting that, given the proper file name, the " [:b "get-post-name"] " function can extract the title of a given post. Simple enough, let's make that test pass by adjusting our view-helpers file:"]
[:pre {:class "brush: clojure"}
"(ns sample-app.view-helpers
  \"Put helper functions for views in this namespace.\"
  (:require [joodo.views :refer [render-partial *view-context*]]
            [chee.string :refer [gsub]]
            [joodo.middleware.asset-fingerprint :refer [path-with-fingerprint]]
            [joodo.env :as env]
            [hiccup.page :refer :all]
            [hiccup.form :refer :all]
            [sample-app.controller.post-controller :refer [blog-post-filenames]]
            [clojure.string :as string :refer [split]]))

(defn get-post-name [post-file-name]
  (gsub
    (second (string/split post-file-name #\"(\\.)|(_)\"))
    #\"-\"
    (fn [_] \" \")))"]
[:p "With that code, we created our " [:b "get-post-name"] " function and told it to split up the post's file name at any periods or underscores. If the file name is formatted correctly, we will extract our post title. Then with that post title, we use chee's gsub function ("[:a {:href "https://github.com/slagyr/joodo"} "chee"] " is a component of Joodo) to replace all dashes with empty spaces. Addionally we listed the blog-post-filenames function in our " [:b "ns :require" ] " section so that when we make our view, it will have access to that function. The " [:b "hiccup.page"] " and " [:b "hiccup.form"] " are included by default to allow your views to use their convenient helper functions. A list of those functions can be found on " [:a {:href "http://weavejester.github.com/hiccup/" :target "_blank"} "hiccup's documentation website"] "."]
[:h4 "Extract the Date from Our File Name"]
[:p "Next we want to extract the date from our post's file name. So let's adjust the view_helper_spec file to look like the following, adding " [:b "get-post-date"] "  to the  " [:b "sample-app.view-helpers"] " 'refer' vector:"]
[:pre {:class "brush: clojure"}
"(ns sample-app.view.view-helpers-spec
  (:require [speclj.core :refer [describe it should= run-specs around]]
            [chee.datetime :refer [parse-datetime]]
            [sample-app.view-helpers :refer [get-post-name get-post-date]]))

(describe \"view_helpers\"
  (it \"gets the title of a post\"
    (should= \"test post 1\"
      (get-post-name \"20111215_test-post-1.hiccup\")))

  (it \"gets the date of a post\"
        (should= (parse-datetime :dense \"20111215000000\")
                       (get-post-date \"20111215_test-post-1.hiccup\"))))
"]
[:p "In this test, we are assuming proper file name formatting as well. However, instead of expecting a string we are expecting a Java Date Object. We are doing this by using the parse-datetime function from " [:a {:href "https://github.com/slagyr/joodo/tree/master/chee"} "Chee"]". We are passing it the value of dense and a string that has the date December 15, 2011 formatted like \"YYYYMMDDHHMMSS\". To make our test pass, let's adjust our view-helper file to match the following:"]
[:pre {:class "brush: clojure"}
"(ns sample-app.view-helpers
  \"Put helper functions for views in this namespace.\"
  (:require [joodo.views :refer [render-partial *view-context*]]
            [chee.string :refer [gsub]]
            [chee.datetime :refer [parse-datetime]]
            [hiccup.page :refer :all]
            [hiccup.form :refer :all]
            [joodo.middleware.asset-fingerprint :refer [path-with-fingerprint]]
            [joodo.env :as env]
            [sample-app.controller.post-controller :refer [blog-post-filenames]]
            [clojure.string :as string :refer [split]]))

(defn- post-parts [post-file-name]
  (string/split post-file-name #\"(\\.)|(_)\"))

(defn get-post-name [post-file-name]
  (gsub
    (second (post-parts post-file-name))
    #\"-\"
    (fn [_] \" \")))

(defn get-post-date [post-file-name]
  (parse-datetime :dense
    (str (first (post-parts post-file-name)) \"000000\")))"]
[:p "As you can see, we added the " [:b "chee.datetime"] " namespace to our list of namespaces that we are using. Then we extracted the post-parts logic into its own function to avoid duplication. And finally, we created the get-post-date function and set it to return a date object with the extracted year, month, and day with zero for the hour, minute, and second values."]
[:h4 "Oder Our Posts According to Date"]
[:p "Lastly, lets add a helper function that puts our posts in order of most recent to oldest. Here is the code that tests our new feature:"]
[:pre {:class "brush: clojure"}
"(ns sample-app.view.view-helpers-spec
  (:require [speclj.core :refer [describe it should= run-specs around with]]
            [chee.datetime :refer [parse-datetime]]
            [sample-app.view-helpers :refer [get-post-name get-post-date
                                                  order-posts]]))

(describe \"view_helpers\"
  (with test-post-1 \"20111215_test-post-1.hiccup\")
  (with test-post-2 \"20111216_test-post-2.hiccup\")
  (with test-post-3 \"20111217_test-post-3.hiccup\")

  (it \"gets the title of a post\"
    (should= \"test post 1\" (get-post-name @test-post-1)))

  (it \"gets the date of a post\"
    (should=
      (parse-datetime :dense \"20111215000000\")
      (get-post-date @test-post-1)))

  (it \"orders posts from most recent to oldest\"
    (should= [@test-post-3 @test-post-2 @test-post-1]
             (order-posts [@test-post-2 @test-post-1 @test-post-3])))
)"]
[:p "As you can see, we refactored the test post string into its own variable to make testing easier. We also added the " [:b "with"] " function to the speclj.core 'refer' vector. Then we created an assertion that tests the order-posts function. The code to make this test pass is pretty simple too:"]
[:pre {:class "brush: clojure"}
"(defn order-posts [post-file-names]
  (sort-by
    #(Integer/parseInt (first (post-parts %)))
    >
    post-file-names))"]
[:p "If you add the order-posts function to the view-helpers.clj file then ran the tests, all of your assertions should pass. All we are doing here is extracting the date from the file name, parsing it into an Integer, then sorting it largest to smallest."]

[:h4 "Retrieve the Route for Each Post"]
[:p "The last thing our views will need is the route each post is associated with. To get started on that feature, add the following test to your view-helpers-spec, and remember to add the new function we're testing " [:b "get-post-route"] "to the 'refer' vector:"]
[:pre {:class "brush: clojure"}
"(it \"gets the route for a specified post\"
  (should= \"/post/20111215_test-post-1\"
           (get-post-route @test-post-1)))"]
[:p "Then to make that test pass, we can add the following function to our view-helpers file."]
[:pre {:class "brush: clojure"}
"(defn get-post-route [post-file-name]
  (let [this-posts-parts (post-parts post-file-name)
        date (first this-posts-parts)
        title (second this-posts-parts)]
    (str \"/post/\" date \"_\" title)))"]
[:p "All this function does is use our existing post-parts function to split up the file name and take the pieces we need when creating our route string."]

[:h4 "Summary"]
[:p "That was a lot.  This is what your files should look like at this point:"]
[:p "view_helper_spec.clj:"]
[:pre {:class "brush: clojure"}
"(ns sample-app.view.view-helpers-spec
  (:require [speclj.core :refer [describe it should= run-specs around with]]
            [chee.datetime :refer [parse-datetime]]
            [sample-app.view-helpers :refer [get-post-name get-post-date
                                             order-posts get-post-route]]))

(describe \"view_helpers\"
  (with test-post-1 \"20111215_test-post-1.hiccup\")
  (with test-post-2 \"20111216_test-post-2.hiccup\")
  (with test-post-3 \"20111217_test-post-3.hiccup\")

  (it \"gets the title of a post\"
    (should= \"test post 1\" (get-post-name @test-post-1)))

  (it \"gets the date of a post\"
    (should=
      (parse-datetime :dense \"20111215000000\")
      (get-post-date @test-post-1)))

  (it \"orders posts from most recent to oldest\"
    (should= [@test-post-3 @test-post-2 @test-post-1]
      (order-posts [@test-post-2 @test-post-1 @test-post-3])))

  (it \"gets the route for a specified post\"
      (should= \"/post/20111215_test-post-1\"
                   (get-post-route @test-post-1))))"
]

[:p "view_helper.clj:"]
[:pre {:class "brush: clojure"}
"(ns sample-app.view-helpers
  \"Put helper functions for views in this namespace.\"
  (:require [joodo.views :refer [render-partial *view-context*]]
            [chee.string :refer [gsub]]
            [chee.datetime :refer [parse-datetime]]
            [hiccup.page :refer :all]
            [hiccup.form :refer :all]
            [joodo.middleware.asset-fingerprint :refer [path-with-fingerprint]]
            [joodo.env :as env]
            [sample-app.controller.post-controller :refer [blog-post-filenames]]
            [clojure.string :as string :refer [split]]))

(defn- post-parts [post-file-name]
  (string/split post-file-name #\"(\\.)|(_)\\\"))

(defn get-post-name [post-file-name]
  (gsub
    (second (post-parts post-file-name))
     #\"-\"
    (fn [_] \" \")))

(defn get-post-date [post-file-name]
  (parse-datetime :dense
                  (str (first (post-parts post-file-name)) \"000000\")))

(defn order-posts [post-file-names]
    (sort-by
          #(Integer/parseInt (first (post-parts %)))
          >
          post-file-names))

(defn get-post-route [post-file-name]
     (let [this-posts-parts (post-parts post-file-name)
                   date (first this-posts-parts)
                   title (second this-posts-parts)]
           (str \"/post/\" date \"_\" title)))"
]


[:h3 "List Our Blog Posts"]

[:p "Now that all of that is done, we can list our blog posts by adding the following code into our index.hiccup file:"]
[:pre {:class "brush: clojure"}
"[:h1 \"Posts\"]
(for [current-post-filename (order-posts (blog-post-filenames))]
  (list
    [:a {:href (get-post-route current-post-filename)}
        (get-post-name current-post-filename)]
    [:span \" - \" (.toString (get-post-date current-post-filename))]
    [:br]))"]
[:p "This is all of the code we will need to list all of our blog posts on our index page. To see this in action, create some blog posts in the src/sample_app/view/posts/ directory with the expected formatting of YYYYMMDD_post-title.hiccup. Then fire up the local server and look at the site in your favorite browser."]
[:p "The last bit of code we want to add is a bit of error handling. Add the following code to the not_found.hiccup file to display the errors specified by our controller:"]
[:pre {:class "brush: clojure"}
"[:h1 \"Not Found\"]
(if-let [error (:error *view-context*)]
  [:p error])"]

[:h3 "Add An Image"]
[:p "Let's add a logo. All images used by a Joodo application are stored in the <b>resources/public/images/</b> directory, and your views are located in src/sample_app/view. The latest version of Joodo uses the .hiccup extenstion for all view files, but also supports the older .hiccup.clj extension."]
[:p "Place an image file with your logo into the images directory and then we can display it by editing our " [:b "src/sample_app/view/layout.hiccup"] " file. Add the following inside the body of your layout file:"]
[:pre {:class "brush: clojure"} "[:img {:src \"/images/logo.png\"}]"]
[:p "As you'll notice, your page is displaying your logo and Joodo's starting page. Delete all of the markup in the src/sample_app/view/index.hiccup file to clean up the page."]

[:br][:br]
[:a {:href "/tutorial/controllers"} "<-- Previous Step (Controllers)"]
[:span " | "]
[:a {:href "/tutorial/reference"} "Next Step (References) -->"]


